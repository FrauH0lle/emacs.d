#!/usr/bin/env sh
:; # -*- mode: emacs-lisp; lexical-binding: t -*-
:; case "$EMACS" in *term*) EMACS=emacs ;; *) EMACS="${EMACS:-emacs}" ;; esac
:; [ "$EMACS" = emacs ] && { type emacs >/dev/null 2>&1 || err=1; }
:; [ -n "$err" ] && { echo "Error: failed to run Emacs with command '$EMACS'"; echo; echo "Are you sure Emacs is installed and in your \$PATH?"; exit 1; } >&2
:; emacs="$EMACS ${DEBUG:+--debug-init} -q --no-site-file --batch"
:; $emacs --load "$0" -- "$@" || exit=$?
:; exit $exit

;; The above shebang does the following magic:

;; 1) #!/usr/bin/env sh
;;
;;    This shebang line tells the system to execute this script using the sh
;;    shell found in the system's PATH.

;; 2) :; # -*- mode: emacs-lisp; lexical-binding: t -*-
;;
;;    This line sets the Emacs Lisp mode and enables lexical binding. The :;
;;    part is a shell and Emacs Lisp compatibility trick. In shell, : is a no-op
;;    command, and ; separates commands. In Emacs Lisp, : is a keyword symbol
;;    literal, and ; starts a comment.

;; 3) :; case "$EMACS" in *term*) EMACS=emacs ;; *) EMACS="${EMACS:-emacs}" ;; esac
;;
;;    This line checks if the EMACS environment variable is set and contains
;;    "term". If so, it sets EMACS to "emacs". Otherwise, it sets EMACS to its
;;    current value or "emacs" if not set.

;; 4) :; [ "$EMACS" = emacs ] && { type emacs >/dev/null 2>&1 || err=1; }
;;
;;    This line sets up the EMACS environment variable to contain the Emacs
;;    command. If the command cannot be found, the err variable is set to 1.

;; 5) [ -n "$err" ] && { echo "Error: failed to run Emacs with command '$EMACS'"; echo; echo "Are you sure Emacs is installed and in your \$PATH?"; exit 1; } >&2
;;
;;    If err is not empty, it prints an error message and exits with status code
;;    1.

;; 6) :; emacs="$EMACS ${DEBUG:+--debug-init} -q --no-site-file --batch"
;;
;;    This line sets up the emacs variable to contain the Emacs command with
;;    appropriate flags. If the DEBUG environment variable is set, the
;;    --debug-init flag is added. The -q flag means no initialization file is
;;    loaded, --no-site-file skips loading site files, and --batch runs Emacs in
;;    non-interactive mode.

;; 7) :; eval "$emacs --load" "$0" -- "$@" || exit=$?
;;
;;    This line runs Emacs, loading the script itself ($0) and passing any
;;    command-line arguments ("$@"). If the command fails, it stores the exit
;;    status in the exit variable.

;; 8) :; exit $exit
;;
;;    Finally, this line exits the script with the exit status stored in the
;;    exit variable.


;; In CLI sessions, prefer correctness over performance.
(setq load-prefer-newer t)

;; Process command line arguments early
;; Remove the -- separator
(pop argv)

;; The core file sets up everything we need
(condition-case e
    (let* ((bin-dir (file-name-directory (file-truename load-file-name)))
           (init-file (expand-file-name "../early-init.el" bin-dir)))
      (or (and (load init-file nil 'nomessage 'nosuffix)
               (featurep 'zenit-core))
          (user-error "Failed to load Emacs from %s" init-file)))
  ;; Prevent ugly backtraces for trivial errors
  (user-error (message "Error: %s" (cadr e))
              (kill-emacs 1)))

;; Abort if the user is using this script as root, unless ~/.emacs.d is owned by
;; root
(when (equal 0 (user-real-uid))
  (unless (equal 0 (file-attribute-user-id (file-attributes zenit-emacs-dir)))
    (message
     (concat
      "Error: this script was executed as root, which is likely not what you want.\n"
      "If this really *is* what you want, then change the owner of your Emacs\n"
      "config to root:\n\n"
      "  chown root:root -R " (abbreviate-file-name zenit-emacs-dir) "\n\n"
      "Aborting..."))
    (kill-emacs 1)))

;; Sort options and switches
(defvar zenit-cli--cmd-switches nil)
(let (subcommand-groups  ; List of (subcommand . flags) pairs
      global-switches
      (option-order zenit-cli-known-subcommands))
  (while argv
    (let ((current-arg (pop argv)))
      (if (member current-arg option-order)
          ;; This is a subcommand - collect it and its flags
          (let ((subcommand-flags nil))
            (while (and argv (not (member (car argv) option-order)))
              (push (pop argv) subcommand-flags))
            ;; Store subcommand with its flags (in original order)
            (push (cons current-arg (nreverse subcommand-flags))
                  subcommand-groups))
        ;; This is a global switch
        (push current-arg global-switches))))

  (setq global-switches (nreverse global-switches))

  ;; Sort subcommand groups by their position in option-order
  (setq subcommand-groups
        (sort subcommand-groups
              (lambda (a b)
                (< (cl-position (car a) option-order :test #'equal)
                   (cl-position (car b) option-order :test #'equal)))))

  ;; Flatten subcommand groups back into a flat list
  (let ((sorted-options nil))
    (dolist (group subcommand-groups)
      (push (car group) sorted-options)  ; subcommand
      (dolist (flag (cdr group))
        (push flag sorted-options)))     ; flags for this subcommand
    (setq sorted-options (nreverse sorted-options))

    (setq zenit-cli--cmd-switches global-switches)
    (setq command-line-args-left (append global-switches sorted-options))))

;; We need to start logging here, otherwise the messages are never shown.
(zenit-log "cli: command line arguments: %s" command-line-args-left)
(zenit-log "cli: zenit-cli--cmd-switches: %s" zenit-cli--cmd-switches)

;; Using `command-switch-alist' and `command-line-functions' turned out to be
;; much more inflexible, especially because of potentially conflicting arguments
;; like --file. In this example, Emacs would prioritize the internal --file
;; command switch which would open the specified file, even if there is another
;; entry in `command-switch-alist'. Thus, we do it on our own.

;; Process command line args
(defvar zenit-cli--cmd-options nil)
(defvar zenit-cli--subcommand-args nil
  "Arguments that may belong to subcommands.")

(while argv
  (let ((arg (pop argv)))
    (cond
     ;; Global switches (these are always processed here)
     ((or (string= arg "--force")
          (string= arg "-F"))
      (setenv "FORCE" "1") (setq zenit-cli-auto-discard t))
     ((string-match "\\`--force=\\(.*\\)\\'" arg)
      (setenv "FORCE" (match-string 1 arg))
      (setq zenit-cli-auto-discard arg))

     ((or (string= arg "--debug")
          (string= arg "-D"))
      (setenv "DEBUG" "2")
      (setq zenit-log-level 2)
      (zenit-debug-mode +1))
     ((string-match "\\`--debug=\\(.*\\)\\'" arg)
      (setenv "DEBUG" (match-string 1 arg))
      (setq zenit-log-level (string-to-number (getenv-internal "DEBUG")))
      (zenit-debug-mode +1))

     ((or (string= arg "--yes")
          (string= arg "-y"))
      (setenv "YES" "1") (setq zenit-cli-auto-accept t))
     ((string-match "\\`--yes=\\(.*\\)\\'" arg)
      (setenv "YES" (match-string 1 arg))
      (setq zenit-cli-auto-accept t))

     ;; We handled --init-directory already in early-init.el and can just ignore
     ;; it here
     ((string= arg "--init-directory")
      (pop argv))
     ((string-match "\\`--init-directory=\\(.*\\)\\'" arg))

     ;; Subcommands
     ((string= arg "deploy")
      (push arg zenit-cli--cmd-options))
     ((string= arg "env")
      (push arg zenit-cli--cmd-options))
     ((string= arg "eval")
      (push arg zenit-cli--cmd-options))
     ((string= arg "sync")
      (push arg zenit-cli--cmd-options))
     ((string= arg "refresh")
      (push arg zenit-cli--cmd-options))
     ((string= arg "freeze")
      (push arg zenit-cli--cmd-options))
     ((string= arg "clean")
      (push arg zenit-cli--cmd-options))
     ((string= arg "clean-straight")
      (push arg zenit-cli--cmd-options))
     ((string= arg "validate")
      (push arg zenit-cli--cmd-options))
     ((string= arg "test")
      (push arg zenit-cli--cmd-options))
     ((string= arg "help")
      (push arg zenit-cli--cmd-options))

     ;; Everything else goes to subcommand args (will be validated later)
     (t (push arg zenit-cli--subcommand-args)))))

(setq zenit-cli--subcommand-args (nreverse zenit-cli--subcommand-args))

(defun zenit-cli-show-help ()
  (print-group!
    (print! (bold "Usage: emacs-config [--SWITCHES] [OPTIONS]\n"))
    (print! (bold "OPTIONS"))
    (print! "%s\t%s" (bold "deploy") "\tDeploy Emacs configuration")
    (print! "%s\t%s" (bold "env") "\t\tGenerate an env file")
    (print! "%s\t%s" (bold "eval") "\t\tEvaluate arbitrary elisp code")
    (print! "%s\t%s" (bold "refresh") (concat "\tRefresh configuration:\n"
                                              "\t\t\t(Re)build packages if necessary and (re)generate autoloads."))
    (print! "%s\t%s" (bold "sync") (concat "\t\tSync package versions and refresh"))
    (print! "%s\t%s" (bold "freeze") "\tFreeze package versions")
    (print! "%s\t%s" (bold "clean") "\t\tClean .elc files and more")
    (print! "%s\t%s" (bold "test") (concat "\t\tTest Emacs configuration"))
    (print! "%s\t%s" (bold "help") "\t\tShow this help message and quit")
    (print! (bold "\nSWITCHES"))
    (print! "%s\t%s" (bold "--debug[=LEVEL]/-D") "Switch on DEBUG mode. Optional LEVEL from 1 to 3 (defaults to 2).")
    (print! "%s\t%s" (bold "--force/-F") "Switch on FORCE")
    (print! "%s\t%s" (bold "--yes/-y") "Answer YES to all prompts"))
  (kill-emacs 0))

;; If no command line arguments are found, show help
(when (null zenit-cli--cmd-options)
  (zenit-cli-show-help))

;; Run commands
(zenit-cli-redirect-output
  (add-hook 'kill-emacs-hook #'zenit-cli--output-write-logs-h 95)
  (zenit-log "run: %s" (combine-and-quote-strings (append zenit-cli--cmd-switches zenit-cli--cmd-options)))

  (setq zenit-cli--cmd-options (nreverse zenit-cli--cmd-options))
  (while zenit-cli--cmd-options
    (let ((option (pop zenit-cli--cmd-options)))
      (cond
       ((string= option "deploy")
        (zenit-cli-with-options
            (:subcommand "deploy" . '((("--config") :type boolean :var config-p :value :yes :default :yes
                                       :help "Create local config dummyfiles")
                                      (("--no-config") :type boolean :var config-p :value :no
                                       :help "Do not create local config dummyfiles")
                                      (("--env") :type boolean :var envfile-p :value :yes :default :yes
                                       :help "(Re)generate an envvars file")
                                      (("--no-env") :type boolean :var envfile-p :value :no
                                       :help "Do not (re)generate an envvars file")
                                      (("--fonts") :type boolean :var fonts-p :value :yes :default :yes
                                       :help "Install nerd-icons fonts")
                                      (("--no-fonts") :type boolean :var fonts-p :value :no
                                       :help "Do not install nerd-icons fonts")))
            zenit-cli--subcommand-args
          (zenit-cli-deploy config-p envfile-p fonts-p)))
       ((string= option "env")
        (zenit-cli-with-options
            (:subcommand "env" . '((("--allow-all") :type boolean :var allow-all :value t
                                    :help "Allow all environment variables")
                                   (("--deny-all") :type boolean :var deny-all :value t
                                    :help "Deny all environment variables")
                                   (("--allow") :type list :var allow-list
                                    :help "Allow specific environment variables (can be repeated)")
                                   (("--deny") :type list :var deny-list
                                    :help "Deny specific environment variables (can be repeated)")
                                   (("-o" "--output") :type file :var output-file
                                    :help "Output file path for the generated env file")))
            zenit-cli--subcommand-args
          (zenit-cli-reload-env-file output-file allow-all deny-all allow-list deny-list)))
       ((string= option "eval")
        (zenit-cli-with-options
            (:subcommand "eval" . '((("-e" "--expr") :type string :var expression
                                     :help "Elisp expression to evaluate (can be repeated)")
                                    (("-f" "--file") :type file :var eval-file
                                     :help "File containing elisp code to evaluate")
                                    (("-p" "--print") :type boolean :var print-result
                                     :help "Print the result of the evaluation")
                                    (("-r" "--raw") :type boolean :var result-only-p
                                     :help "Only print the result of the evaluation")))
            zenit-cli--subcommand-args
          (zenit-cli-eval expression eval-file print-result result-only-p)))
       ((string= option "sync")
        (zenit-cli-with-options
            (:subcommand "sync" . '((("-e") :type boolean :var noenvvar-p
                                     :help "Don't regenerate the envvar file")
                                    (("-b" "--rebuild") :type boolean :var rebuild-p
                                     :help "Rebuild all installed packages, unconditionally")
                                    (("-B") :type boolean :var nobuild-p
                                     :help "Don't rebuild installed packages")
                                    (("-u") :type boolean :var update-p
                                     :help "Update all installed packages")
                                    (("-U") :type boolean :var noupdate-p
                                     :help "Don't update any packages")
                                    (("--reclone") :type boolean :var reclone-p
                                     :help "Reclone packages")))
            zenit-cli--subcommand-args
          (zenit-cli-sync noenvvar-p update-p noupdate-p reclone-p rebuild-p nobuild-p)))
       ((string= option "refresh")
        (zenit-cli-with-options
            (:subcommand "refresh" . '((("-e") :type boolean :var noenvvar-p
                                        :help "Don't regenerate the envvar file")
                                       (("-b" "--rebuild") :type boolean :var rebuild-p
                                        :help "Rebuild all installed packages, unconditionally")
                                       (("-B") :type boolean :var nobuild-p
                                        :help "Don't rebuild installed packages")))
            zenit-cli--subcommand-args
          (zenit-cli-refresh noenvvar-p rebuild-p nobuild-p)))
       ((string= option "freeze")
        (zenit-cli-freeze-packages))
       ((string= option "clean")
        (zenit-cli-with-options
            (:subcommand "clean" . '((("--straight") :type boolean :var cleanstraight-p
                                      :help "Delete straight's repository and build directory")
                                     (("--all") :type boolean :var cleanall-p
                                      :help "Delete all repository and build directories")))
            zenit-cli--subcommand-args
          (zenit-cli-clean-compiled-files)
          (when cleanstraight-p
            (delete-directory
             (file-truename
              (expand-file-name (file-name-concat zenit-emacs-dir "straight" "build" "straight.el")))
             t)
            (delete-directory
             (file-truename
              (expand-file-name (file-name-concat zenit-emacs-dir "straight" "repos" "straight")))
             t))
          (when cleanall-p
            (delete-directory
             (file-truename
              (expand-file-name (file-name-concat zenit-emacs-dir "straight" "build")))
             t)
            (delete-directory
             (file-truename
              (expand-file-name (file-name-concat zenit-emacs-dir "straight" "repos")))
             t))
          (when (or cleanstraight-p cleanall-p)
            (let ((straight-build-cache-file
                   (file-truename
                    (expand-file-name (file-name-concat zenit-emacs-dir "straight" "build-cache.el")))))
              (when (file-exists-p straight-build-cache-file)
                (delete-file straight-build-cache-file))))
          (print! (warn "Now run 'emacs-config refresh' or 'emacs-config sync'"))))
       ((string= option "test")
        (zenit-cli-with-options
            (:subcommand "test" . '((("-f" "--file") :type list :var test-files
                                     :help "Use this test file (can be repeated)")))
            zenit-cli--subcommand-args
          (zenit-cli-test test-files)))
       ((string= option "help")
        (zenit-cli-show-help))))))

(kill-emacs 0)
