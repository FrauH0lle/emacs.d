;; lisp/core/cli/env.el -*- lexical-binding: t; no-byte-compile: t; -*-

(defvar zenit-env-blacklist
  '(;; Unix/shell state that shouldn't be persisted
    "^HOME$" "^\\(OLD\\)?PWD$" "^SHLVL$" "^PS1$" "^R?PROMPT$" "^TERM\\(CAP\\)?$"
    "^USER$" "^GIT_CONFIG" "^INSIDE_EMACS$"
    ;; X server, Wayland, or services' env  that shouldn't be persisted
    "^\\(WAYLAND_\\)?DISPLAY$" "^DBUS_SESSION_BUS_ADDRESS$" "^XAUTHORITY$"
    ;; Windows+WSL envvars that shouldn't be persisted
    "^WSL_INTEROP$"
    ;; XDG variables that are best not persisted.
    "^XDG_CURRENT_DESKTOP$" "^XDG_RUNTIME_DIR$"
    "^XDG_\\(VTNR$\\|SEAT$\\|BACKEND$\\|SESSION_\\)"
    ;; Socket envvars, like I3SOCK, GREETD_SOCK, SEATD_SOCK, SWAYSOCK, NIRI_SOCKET, etc.
    "SOCK\\(ET\\)?$"
    ;; ssh and gpg variables that could quickly become stale if persisted.
    "^SSH_\\(AUTH_SOCK\\|AGENT_PID\\)$" "^\\(SSH\\|GPG\\)_TTY$"
    "^GPG_AGENT_INFO$"
    ;; Internal envvars
    "^DEBUG$" "^FORCE$" "^YES$" "^INSECURE$" "^\\(EMACS\\|ZENIT\\(LOCALCONF\\)?\\)DIR$"
    "^__")
  "Environment variables to omit from envvar files.

Each string is a regexp, matched against variable names to omit from
`zenit-env-file'.")

(defvar zenit-env-whitelist '()
  "A whitelist for envvars to save in `zenit-env-file'.

This overrules `zenit-env-blacklist'. Each string is a regexp, matched
against variable names to include in `zenit-env-file'.")

;;;###autoload
(defun zenit-cli-reload-env-file (&optional env-file allow-only deny-only allowlist denylist)
  "Generates `zenit-env-file', if it doesn't exist (or if FORCE-P).

This scrapes the variables from your shell environment by running
`zenit-env-executable' through `shell-file-name' with
`zenit-env-switches'. By default, on Linux, this is '$SHELL -ic
/usr/bin/env'. Variables in `zenit-env-blacklist' are removed."
  (let ((env-file (expand-file-name (or env-file zenit-env-file))))
    (with-temp-file env-file
      (setq-local coding-system-for-write 'utf-8-unix)
      (print! (start "%s envvars file at %S")
              (if (file-exists-p env-file)
                  "Regenerating"
                "Generating")
              (path env-file))
      (print-group!
        (goto-char (point-min))
        (insert
         (concat
          ";; -*- mode: lisp-interaction; coding: utf-8-unix -*-\n"
          (format ";; Generated from a '%s' shell environent\n" shell-file-name)
          ";; ----------------------------------------------------------------------------\n"
          ";; This file was auto-generated by `emacs-config env'. It contains a list of\n"
          ";; environment variables scraped from your default shell (based on your\n"
          ";; settings for `zenit-env-blacklist' and `zenit-env-whitelist').\n"
          ";;\n"
          (if (file-equal-p env-file zenit-env-file)
              (concat ";; It is NOT safe to edit this file. Changes will be overwritten next time you\n"
                      ";; run 'emacs-config refresh/sync'. To create a safe-to-edit envvar file use:\n;;\n"
                      ";;   emacs-config env -o ~/.emacs.d/site-lisp/myenv\n;;\n"
                      ";; And load it with (zenit-load-envvars-file \"~/.emacs.d/site-lisp/myenv\").\n")
            (concat ";; This file is safe to edit by hand, but needs to be loaded manually with:\n;;\n"
                    ";;   (zenit-load-envvars-file \"path/to/this/file\")\n;;\n"
                    ";; Use 'emacs-config env -o path/to/this/file' to regenerate it.\n"))
          "\n"))
        ;; We assume that this noninteractive session was spawned from the
        ;; user's interactive shell, therefore we just dump
        ;; `process-environment' to a file.
        ;;
        ;; Still, this should be well-formatted, in case humans want to
        ;; hand-modify it.
        (let ((blacklist (remq nil (append (if deny-only  '(".")) zenit-env-blacklist)))
              (whitelist (remq nil (append (if allow-only '(".")) zenit-env-whitelist))))
          (dolist (rule (append allowlist denylist))
            (push rule (if (member rule allowlist)
                           whitelist
                         blacklist)))
          (insert "(")
          (dolist (env (get 'process-environment 'initial-value))
            (catch 'skip
              (let* ((var  (car (split-string env "=")))
                     (pred (zenit-rpartial #'string-match-p var)))
                (when (seq-find pred blacklist)
                  (if (seq-find pred whitelist)
                      (zenit-log "cli:env: whitelisted %s" var)
                    (zenit-log "cli:env: ignored %s" var)
                    (throw 'skip t)))
                (insert (prin1-to-string env) "\n "))))
          (insert ")\n"))
        (print! (success "Successfully generated %S") (path env-file))
        t))))
